/**
 * JavaCC file
 */
 
options {
  JDK_VERSION = "1.5";
}
PARSER_BEGIN(ScheduleParser)
package cz.muni.fi.spc.SchedVis.parsers.schedule;

import cz.muni.fi.spc.SchedVis.parsers.Parser;

public class ScheduleParser extends Parser {
  public static void main(String args[]) throws ParseException {
      try {
        ScheduleParser parser = new ScheduleParser(System.in);
        ScheduleEventsList events = parser.read();
        System.out.println("Successfully read " + events.size() + " events!");
      } catch (Exception e) {
        System.out.println("NOK.");
        System.out.println(e.getMessage());
      } catch (Error e) {
        System.out.println("Oops.");
        System.out.println(e.getMessage());
      }
  }
}
PARSER_END(ScheduleParser)

TOKEN :
{
    < CONSTANT: "-1" | ( <DIGIT> )+ >  /* A numeric constant. */
|   < CONSTANT_LIST: ( <CONSTANT> ( "," <CONSTANT> )+ ) >  /* A numeric constant. */
|   < #DIGIT: ["0" - "9"] >     /* A single number. */
|   < STRING: ["a"-"z","A"-"Z"] ( ["a"-"z","A"-"Z","_","0"-"9"] )* > /* An allowed string. */
|   < WHITESPACE: ([" ", "\t"])+ > /* An allowed whitespace. */
}

TOKEN :
{
	< MOVE_EVENT_FLAG: "machine-failure-move-good"|"machine-failure-move-bad"|"good-move"|"bad-move">
    |< MACHINE_EVENT_FLAG: "machine-failure" | "machine-restart">
    |< IO_EVENT_FLAG: "job-arrival" | "job-execution-start" | "job-cancel" | "job-completion">
}

ScheduleEventsList read() : {
	ScheduleEventsList events = new ScheduleEventsList();
}
{
  (events=event_data(events))+
  ("\n")*
  <EOF>
  {return events;}
}

ScheduleEventsList event_data(ScheduleEventsList events) : {}
{
  events=machine_event(events) (<WHITESPACE>)* "\n" {return events;}
  | events=move_event(events) (<WHITESPACE>)* "\n" {return events;}
  | events=IO_event(events) (<WHITESPACE>)* "\n" {return events;}
}

ScheduleEventsList machine_event(ScheduleEventsList list) : {
    Token event;
    Token clock;
    Token machine;
}
{
	event=<MACHINE_EVENT_FLAG> <WHITESPACE> 
	clock=<CONSTANT> <WHITESPACE>
	machine=<STRING>
	{
        list.add(new ScheduleEventMachine(event, clock, machine));
        return list;
    } 	
}

ScheduleEventsList move_event(ScheduleEventsList list) : {
	Token event;
	Token clock;
	Token job;
	Token origMachine;
	Token newMachine;
	ScheduleMachineDataList data;
}   
{
    event=<MOVE_EVENT_FLAG> <WHITESPACE> 
    clock=<CONSTANT> <WHITESPACE> 
    job=<CONSTANT> <WHITESPACE> 
    origMachine=<STRING> <WHITESPACE>
    newMachine=<STRING> 
    data=schedule_data()
    {
        list.add(new ScheduleEventMove(event, clock, job, origMachine, newMachine, data));
        return list;
    }
}

ScheduleEventsList IO_event(ScheduleEventsList list) : {
    Token event;
    Token clock;
    Token job;
    ScheduleMachineDataList data;
}
{
    event=<IO_EVENT_FLAG> <WHITESPACE> 
    clock=<CONSTANT> <WHITESPACE> 
    job=<CONSTANT> 
    data=schedule_data()
    {
    	list.add(new ScheduleEventIO(event, clock, job, data));
        return list;
    } 
}

ScheduleMachineDataList schedule_data() : {
	ScheduleMachineDataList data = new ScheduleMachineDataList();
}
{
	(<WHITESPACE> data=machine_data(data))+
	{return data;}
}

ScheduleMachineDataList machine_data(ScheduleMachineDataList list) : {
	ScheduleJobDataList data = new ScheduleJobDataList();
	Token machineId;
}
{
	"<" machineId=<STRING> ("|" data=job_data(data))* ">"
	{
		list.add(new ScheduleMachineData(machineId, data));
		return list;
	}
}

ScheduleJobDataList job_data(ScheduleJobDataList list) : {
	Token job;
	Token numCPUs;
	Token assignedCPUs;
	Token arch;
	Token memory;
	Token space;
	Token start;
    Token end;
    Token deadline;
    ScheduleJobData data = new ScheduleJobData();
}
{
	job=<CONSTANT> ";" 
	numCPUs=<CONSTANT> ";" 
	(assignedCPUs=<CONSTANT_LIST> | assignedCPUs=<CONSTANT>) ";" 
	arch=<STRING> ";" 
	memory=<CONSTANT> ";" 
    space=<CONSTANT> ";" 
	start=<CONSTANT> ";"
	end=<CONSTANT> ";" 
	deadline=<CONSTANT>
	(";")*
	{
		data.setId(job);
		data.setNeedsCPUs(numCPUs);
		data.assignCPUs(assignedCPUs);
		data.setArch(arch);
		data.setNeedsMemory(memory);
		data.setNeedsSpace(space);
		data.setStarts(start);
		data.setEnds(end);
		data.setDeadline(deadline);
		list.add(data);
		return list;
	}
}